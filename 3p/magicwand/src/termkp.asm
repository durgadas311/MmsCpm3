; Magic Wand - KayPro
***********************************************
**	       T E R M I N A L		     **
***********************************************
*		Kaypro VERSION

*	DEVELOPED BY SMALL BUSINESS APPLICATIONS INC
*		     3220 LOUISIANA, SUITE 205
*		     HOUSTON, TEXAS  77006
*		     (713)528-5158
*
*	LAST REVISION 10/13/84 by DRM
*
BASE	EQU	0	;STANDARD CP/M BASE

	ORG	BASE+108H
	JMP	0
	JMP	0
	DB	128

	ORG	BASE+200H  ;TERMINAL MODULE ORIGIN
*
ROWSX	EQU	24	;DISPLAY ROWS
COLSX	EQU	80	;DISPLAY COLUMNS
TERMDIM DB	ROWSX,COLSX	;PHYSICAL TERMINAL LIMITS
SCRDIM	EQU	$		;CURRENT SCREEN LIMITS
ROWS	DB	ROWSX-1
COLS	DB	COLSX
CURSOR	EQU	$	;CURRENT CURSOR LOCATION
ROW	DB	1
COL	DB	1
CONOUT	DB	0,0,0	;CONOUT ADDR TO BE SUPPLIED AT INITIALIZATION
ZPRSEQ	JMP	PRSEQ	;OUTPUT CHAR SEQUENCE AT DE UNTIL NULL
ZCURSE	JMP	CURSE	;POSITION CURSOR AT HL
ZCLRSCR JMP	CLRSCRN ;CLEAR WHOLE SCREEN
ZCLRLN	JMP	CLRLINE ;CLEAR REMAINING LINE
ZSCROLL JMP	SCROLL	;SCROLL 1 LINE UP
ZBKSP	JMP	BKSP	;BACKSPACE 1 FROM CURRENT COLUMN
ZALARM	JMP	ALARM	;SOUND ALARM BELL, IF ANY
ZINIT	JMP	INITSCRN ;INITIALIZE TERMINAL, IF NEEDED
ZNAME	DB	'KayPro          '
ZID	DB	'KP'	;TERMINAL ID NUMBER FOR SERIAL
*
*********CONTROL TABLES
*
CTAB	DB	8,16,24,32,40,48,56,64,72,COLSX,0,0,0,0,0,0
	DB	255	;TAB TABLE STOP BYTE
*
*	HIGH-ORDER BIT (128+) ON INDICATES ESCAPE SEQUENCE
*
XTAB	EQU	$	;TABLE OF CONTROL CHARACTERS
XRET	DB	RETC	;CARRIAGE RETURN
XCURR	DB	'D'-40H ;*****	CURSOR RIGHT
XCURU	DB	'E'-40H ;*****	CURSOR UP
XCURD	DB	'X'-40H ;*****	CURSOR DOWN
XDELTL	DB	'U'-40H ;*DL*	LINE DELETE
XFSCRL	DB	'O'-40H ;'F2'	LINE SCROLL FORWARD
XBSCRL	DB	'N'-40H ;'F1'	LINE SCROLL BACKWARD
XPFSCRL DB	'F'-40H ;'F4'	PAGE FORWARD
XPBSCRL DB	'G'-40H ;'F3'	PAGE BACKWARD
XHOME	DB	'A'-40H ;*HOME*	HOME LINE, SCREEN
XSCRTOP DB	'T'-40H	;Ctl-T	TOP OF TEXT
XSCRBOT DB	'B'-40H	;Ctl-B	BOTTOM OF TEXT
XTABC	DB	TABKEY	;CTL-I (TAB KEY)
XISRT	DB	'P'-40H ;*IC*	CHARACTER INSERT
XISRTP	DB	'Q'-40H ;*IL*	FULL INSERT (OPEN/CLOSE TEXT)
XISRTX	DB	127	;*DELETE* WORD DELETE
XESC	DB	ESC	;ESCAPE KEY
XDUPE	DB	'W'-40H ;'Red'	REPEAT SEARCH/REPLACE
XFIND	DB	'V'-40H ;'Blue' START SEARCH/REPLACE
XSETMK	DB	'R'-40H ;'Gray' SET BLOCK MARKER
XFORM	DB	'L'-40H ;'f5'	SET FORM FEED
XLNFD	DB	'J'-40H	;LINE	SET LINE FEED
XBKSP	DB	'H'-40H	;BACKSPACE
XDELTC	DB	'Y'-40H	;*DC*  CHARACTER DELETE
XCURL	DB	'S'-40H ;*****	CURSOR LEFT
XEND	DB	ESC	;END TABLE /CURRENT CHAR
	PAGE
*	TABLE OF CONTROL CODE/DISPLAY SUBSTITUTIONS
SUBCTLS EQU	$	;TRANSLATES VALUES 0-1F
	DB	0,'_',0,0,0,0,0,BELL,0,0
	DB	'|'	;LINE FEED
	DB	0
	DB	'^'	;FORM FEED
XDCR	DB	'~'	;CARRIAGE RETURN
	DB	0,0,0
	DB	':'	;FIND-KEY
	DB	0,0,0,0,0,0,0,0,0,0,0,0,0
*
BLKMARK EQU	1	;BLOCK MARKER
RETC	EQU	13	;LINE-END CTL CHAR
LNFD	EQU	10	;LINE FEED
FFORM	EQU	12	;FORM FEED
TABKEY	EQU	9	;TAB KEY (CONTROL-I)
BELL	EQU	7	;BELL CODE
ESC	EQU	27	;ESCAPE KEY
DELKEY	EQU	127	;DELETE KEY
DEFIND	EQU	7	;KEY TO DEFINE SEARCH/REPLACE
	PAGE
********	KAYPRO SCREEN CONTROL ROUTINES
**
LEADIN	EQU	27	;CONTROL SEQUENCE FLAG CODE
CURSXY	EQU	1F1FH	;CONSTANT TO ADJUST CURSOR CODES
SETCCOD EQU	'='	;SET CURSOR LOC CODE
XCLEAR	EQU	'Z'-40H	;SCREEN CLEAR CODE
LCLEAR	EQU	'X'-40H	;LINE CLEAR CODE
INTSEQ:
	DB	0	;SEQUENCE TERMINATOR
EXITSEQ:               ;return terminal to switches-configuration
	DB	0
CLRSEQ			;SEQUENCE TO CLEAR SCREEN
	DB	XCLEAR
	DB	0
LNSEQ			;SEQUENCE TO CLEAR LINE
	DB	LCLEAR
	DB	0
SETSEQ	DB	LEADIN	;SEQUENCE TO SET CURSOR LOCATION
	DB	SETCCOD
SETC	DW	0	;CURSOR COORDINATES (COL,ROW)
	DB	0
SCROLSQ DB	LEADIN	;SEQUENCE TO SCROLL FWD 1 LINE
	DB	SETCCOD ;FIRST SET CURSOR
	DB	55,32	;TO ROW 24 COL 1
	DB	LNFD	;THEN ISSUE LINE FEED
	DB	LEADIN	;THEN RESET
	DB	SETCCOD ;CURSOR
	DB	54,32	;TO LAST WINDOW ROW
	DB	LCLEAR	;CLEAR IT
	DB	0
*
CURSE	EQU	$	;SET CURSOR CORRDINATES IN HL
	LXI	D,CURSXY	;APPLY ADJUSTMENT TO CODES
	DAD	D	;TO MATCH HARDWARE (POS+20H BASED 0)
	SHLD	SETC	;STORE IN SEQUENCE
	LXI	D,SETSEQ ;USE CPM BUFFER PRINT
	CALL	PRSEQ
	RET
*
SCROLL	LXI	D,SCROLSQ	;INITIATE LINE SCROLL SEQUENCE
	JMP	PRSEQ
*
CLRLINE LXI	D,LNSEQ ;CLEAR LINE
	JMP	PRSEQ
*
BKSP	MVI	C,BKSPCODE
	CALL	CONOUT
	RET
BKSPCODE EQU	8
*
CLRSCRN LXI	H,0101H ;CLEAR WHOLE SCREEN
	CALL	CURSE	;FORCE CURSOR HOME
	LXI	D,CLRSEQ ;CLEAR SCREEN
	JMP	PRSEQ
*
ALARM	MVI	C,BELL	  ;SOUND ALARM BELL
	CALL	CONOUT
	RET
*
PRSEQ	LDAX	D	;PRINT SEQ AT DE UNTIL ZERO
	ANA	A
	RZ
	PUSH	D
	MOV	C,A
	CALL	CONOUT
	POP	D
	INX	D
	JMP	PRSEQ

INITSCRN EQU	$	;INIT/DE-INIT ENVIRONMENT
	LXI	D,EXITSEQ
	LXI	H,INITSW
	MOV	A,M
	MVI	M,-1
	ANA	A
	JNZ	EXIT
	LHLD	ORGCONST
	SHLD	CONST
	LXI	H,TCONST
	SHLD	ORGCONST
	LHLD	ORGCONIN
	SHLD	CONIN
	LXI	H,TCONIN
	SHLD	ORGCONIN
	LXI	D,INTSEQ
EXIT	CALL	PRSEQ
	RET

INITSW	DB	0	;FIRST-TIME SWITCH

ORGCONST EQU	BASE+109H ;MW CONST ADDR FROM CBIOS
CONST	DW	0	;ADDRESS FOR CBIOS CHAR STATUS

ORGCONIN EQU	BASE+10CH ;MW CONIN ADDR FROM CBIOS
CONIN	DW	0	;ADDR FOR CBIOS CHAR INPUT

HLCALL	PCHL		;ROUTINE PROVIDING INDIRECT CALLS

TCONST	LHLD	CONST
	CALL	HLCALL
	RET

TCONIN	LHLD	CONIN
	CALL	HLCALL
	RET

	END
