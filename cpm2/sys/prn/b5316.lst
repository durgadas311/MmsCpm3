		
		
		; May 20 1982	LWF
		********** CP/M LINKABLE BOOT ROUTINE **********
		********** 5.25" MMS CONTROLLER BOOT  **********
		
			TITLE	'BOOT5 --- FDC 5.25" boot module v2.240'
		
0000  0001FFFF		DW	MODLEN,(-1)
		
0000          	BASE	EQU	0000H	;ORG FOR RELOC
		
			MACLIB Z80
**** Z80.lib ****
**** b5316.asm ****
			$-MACRO
		
		***** PHYSICAL DRIVES ARE ASSIGNED AS FOLLOWS *****
		*****					      *****
		*****  33 = FIRST 5.25" DRIVE                 *****
		*****  34 = SECOND 5.25" DRIVE                *****
		*****  35 = THIRD 5.25" DRIVE                 *****
		*****  36 = FOURTH 5.25" DRIVE                *****
		*****					      *****
		***************************************************
		
		
		
		***************************************************
		**  PORTS AND CONSTANTS
		***************************************************
0038          	CTRL	EQU	38H
003C          	WD1797	EQU	3CH
003C          	STAT	EQU	WD1797+0
003D          	TRACK	EQU	WD1797+1
003E          	SECTOR	EQU	WD1797+2
003F          	DATA	EQU	WD1797+3
0009          	SPT	EQU	9		; 9 (512 BYTE) SECTORS ON 5.25"DD
		
00F2          	?PORT	  EQU	0F2H
		
00D0          	?AUX	  EQU	0D0H
00E0          	?PRINTER  EQU	0E0H
00D8          	?MODEM	  EQU	0D8H
		
0003          	?LINE$CTL EQU	00000011B	; NO PARITY, 1 STOP BIT, 8 DATA BITS
000F          	?MOD$CTL  EQU	00001111B	; SET ALL CONTROL LINES TO 'READY'
		
0006          	?S19200 EQU	6	; 19,200 BAUD
000C          	?S9600	EQU	12	;  9,600 BAUD
0018          	?S4800	EQU	24	;  4,800 BAUD
0030          	?S2400	EQU	48	;  2,400 BAUD
0060          	?S1200	EQU	96	;  1,200 BAUD
00C0          	?S600	EQU	192	;    600 BAUD
0180          	?S300	EQU	384	;    300 BAUD
0300          	?S150	EQU	768	;    150 BAUD
0417          	?S110	EQU	1047	;    110 BAUD
		***************************************************
		
		***************************************************
		** LINKS TO REST OF SYSTEM
		***************************************************
1600          	@BIOS	EQU	BASE+1600H
0800          	@BDOS	EQU	@BIOS-0E00H
		***************************************************
		
		***************************************************
		** PAGE ZERO ASSIGNMENTS
		***************************************************
0000          		ORG	0
0000          	?CPM		DS	3
0003          	?DEV$STAT	DS	1
0004          	?LOGIN$DSK	DS	1
0005          	?BDOS		DS	3
0008          	?RST1		DS	3
000B          	?CLOCK		DS	2
000D          	?INT$BYTE	DS	1
000E          	?CTL$BYTE	DS	1
000F          			DS	77
005C          	?FCB		DS	36
0080          	?DMA		DS	128
		?TPA		DS	0
		***************************************************
		
		
		
		***************************************************
		** START OF RELOCATABLE DISK BOOT MODULE
		*************************************************** 
		
2280          		ORG	2280H
		
2280  C38522  	BOOT:	JMP	AROUND
		
2283  00      	SECTRS	DB	0		; NUMBER OF SECTORS TO BOOT, FROM MOVCPM.COM
						; PATCHED DURING EXECUTION OF MOVCPM
2284  21      	DRIVE	DB	33		; ALSO PATCHED BY ASSIGN PROGRAM
		
2285          	AROUND:
2285  E1      		POP	H		; ERROR ROUTINE ADDRESS
2286  310024  		LXI	SP,?STACK
2289  E5      		PUSH	H
		
		***************************************************
		*** START OF UNIQUE ROUTINE FOR BOOTING
		***************************************************
		
228A  3A8422  		LDA	DRIVE
228D  D621    		SUI	33		; BOOT ONLY FROM 5.25" DRIVES
228F  D8      		RC
2290  FE04    		CPI	4		; MAX. 4 DRIVES ONLY
2292  D0      		RNC
2293  F62C    		ORI	00101100B	; TURN ON MOTOR, INTRQ, DRQ, DDEN
2295  328422  		STA	DRIVE
2298  D338    		OUT	CTRL		; SEND TO CONTROL REGISTER
229A  3A8322  		LDA	SECTRS		; MAINTAIN FAST BOOTING,
229D  C605    		ADI	00000101B	; ROUND UP (INCLUDE BOOT SECTORS)
229F  E6FC    		ANI	11111100B	; INTEGER DIVISION
22A1  0F      		RRC
22A2  0F      		RRC			; BY 4
22A3  5F      		MOV	E,A		; NUMBER OF SECTORS TO READ
22A4  013F00  		LXI	B,DATA		; C = INPUT DATA PORT  B=0
22A7  210007  		LXI	H,@BDOS-256	; -256 FOR BOOT
22AA  3E0B    		MVI	A,00001011B	; RESTORE HEAD TO TRACK 0
22AC  CD4523  	LOOP0:	CALL	COMMAND 	; STEP DRIVE
22AF  DB3C    		IN	STAT
22B1  E699    		ANI	10011001B
22B3  C0      		RNZ
22B4  1601    		MVI	D,1
22B6  E5      	SECL0:	PUSH	H		; SAVE MEMORY ADDRESS
22B7  7A      		MOV	A,D
22B8  D33E    		OUT	SECTOR
22BA  3E88    		MVI	A,10001000B	; READ SINGLE SECTOR
22BC  CD4A23  		CALL	READ$REC
22BF  E6BF    		ANI	10111111B	; SET PSW/Z TO INDICATE ERROR STATUS
22C1  280E    		JRZ	OK		; ALL'S WELL IF ZERO
22C3  E1      		POP	H
22C4  3A4423  		LDA	ERRF		; SEE IF THIS IS THE SECOND TRY
22C7  B7      		ORA	A
22C8  C0      		RNZ			; RETURN TO MONITOR PROM
22C9  2F      		CMA
22CA  324423  		STA	ERRF		; ALLOW RETRY ONLY ONCE
22CD  3E4B    		MVI	A,01001011B	; STEP-IN WITHOUT UPDATE (FOR 80 TRK DRIVE)
22CF  18DB    		JR	LOOP0
22D1  E3      	OK:	XTHL			; NEW MEMORY ADDRESS
22D2  E1      		POP	H
22D3  2F      		CMA
22D4  324423  		STA	ERRF		; PREVENT FURTHER RETRY ON THIS TRACK
22D7  1D      		DCR	E		; COUNT A SECTOR READ
22D8  280E    		JRZ	DONE		; STOP IF ALL SECTORS READ
22DA  14      		INR	D		; COUNT ONE SECTOR ON THIS TRACK
22DB  7A      		MOV	A,D
22DC  FE0A    		CPI	SPT+1
22DE  38D6    		JRC	SECL0		; LOOP IF MORE ON THIS TRACK
22E0  AF      		XRA	A
22E1  324423  		STA	ERRF		; RESET FLAG FOR NEW TRACK
22E4  3E5B    		MVI	A,01011011B	; STEP-IN WITH UPDATE
22E6  18C4    		JR	LOOP0		; GO READ SECTOR ZERO OF NEXT TRACK
22E8          	DONE:
		
		***************************************************
		** START OF SYSTEM INITIALIZATION
		*************************************************** 
22E8  F3      		DI
		* INITIALIZE I/O, ETC
22E9  AF      		XRA	A
22EA  D3D4    		OUT	?AUX+4
22EC  D3E4    		OUT	?PRINTER+4
22EE  D3DC    		OUT	?MODEM+4
22F0  3E03    		MVI	A,?LINE$CTL
22F2  F680    		ORI	10000000B	; ENABLE DIVISOR LATCH
22F4  D3D3    		OUT	?AUX+3
22F6  D3E3    		OUT	?PRINTER+3
22F8  D3DB    		OUT	?MODEM+3
		* BAUD RATE SETUP:
22FA  210C00  		LXI	H,?S9600	; AUX SERIAL @ 9600 BAUD
22FD  7D      		MOV	A,L
22FE  D3D0    		OUT	?AUX
2300  7C      		MOV	A,H
2301  D3D1    		OUT	?AUX+1
2303  210C00  		LXI	H,?S9600	; PRINTER @ 9600 BAUD
2306  7D      		MOV	A,L
2307  D3E0    		OUT	?PRINTER
2309  7C      		MOV	A,H
230A  D3E1    		OUT	?PRINTER+1
230C  218001  		LXI	H,?S300 	; MODEM (PAPER TAPE) @ 300 BAUD
230F  7D      		MOV	A,L
2310  D3D8    		OUT	?MODEM
2312  7C      		MOV	A,H
2313  D3D9    		OUT	?MODEM+1
		* NOW GET PORTS READY FOR I/O
2315  3E03    		MVI	A,?LINE$CTL	; NOW DE-SELECT DIVISOR LATCH
2317  D3D3    		OUT	?AUX+3
2319  D3E3    		OUT	?PRINTER+3
231B  D3DB    		OUT	?MODEM+3
231D  3E0F    		MVI	A,?MOD$CTL	; SIGNAL 'READY'
231F  D3D4    		OUT	?AUX+4
2321  D3E4    		OUT	?PRINTER+4
2323  D3DC    		OUT	?MODEM+4
2325  DBD5    		IN	?AUX+5		; RESET ANY STRAY ACTIVITY
2327  DBE5    		IN	?PRINTER+5
2329  DBDD    		IN	?MODEM+5
232B  DBD0    		IN	?AUX
232D  DBE0    		IN	?PRINTER
232F  DBD8    		IN	?MODEM
		* END OF I/O INITIALIZATION
2331  213D23  		LXI	H,?CODE 	; SEQUENCE TO MOVE MEMORY-MAP
2334  0607    		MVI	B,?CODE$LEN	; NUMBER OF BYTES IN SEQUENCE
2336  0EF2    		MVI	C,?PORT 	; I/O PORT TO SEND SEQUENCE
2338  EDB3    		OUTIR
233A  C30016  		JMP	@BIOS
233D  04      	?CODE	DB	0000$01$00B
233E  0C      		DB	0000$11$00B
233F  04      		DB	0000$01$00B
2340  08      		DB	0000$10$00B
2341  0C      		DB	0000$11$00B
2342  08      		DB	0000$10$00B
2343  22      		DB	0010$00$10B	; FOR "-FA" MACHINES
0007          	?CODE$LEN	EQU	$-?CODE
		
2344  00      	ERRF:	DB	0	; ERROR FLAG FOR CONTROL OF 80 TRACK SITUATION.
					; IF A 40-TRACK DISK IS BOOTED IN AN 80-TRACK
					; DRIVE, EACH TRACK REQUIRES TWO STEPS, BUT
					; THE TRACK REGISTER IS UPDATED ONLY ONCE.
2345          	COMMAND:
2345  D33C    		OUT	STAT
2347  FB      		EI
2348  18FE    		JR $-1
		
234A          	READ$REC:
234A  D33C    		OUT	STAT
234C  FB      		EI
234D  76      	RD0	HLT
234E  EDA2    		INI
2350  C24D23  		JNZ	RD0
2353  76      	RD1	HLT
2354  EDA2    		INI
2356  C25323  		JNZ	RD1
2359  18FE    		JR $-1
		
235B  00000000		REPT	256-($-BOOT)-1
      00000000
      00000000
      00000000
      00000000
      00000000
      00000000
      00000000
      00000000
		
237F          	RETRY:				; LOCATION HAS DUAL-FUNCTION
237F  07      	ID	DB	7		; BOOT ROUTINE IDENTIFICATION
0100          	MODLEN	EQU	$-BOOT		; MUST BE 256 BYTES
2400          	?STACK: EQU	$+128
		
2380  00000000	 DB 00000000B,00000000B,00000000B,00000000B,00000000B,01000000B,00000000B,00000000B
      00400000
2388  00000000	 DB 00000000B,00000000B,00000000B,00000000B,00000000B,00000000B,00000000B,00000000B
      00000000
2390  00000000	 DB 00000000B,00000000B,00000000B,00000000B,00000000B,00000000B,00000000B,00001000B
      00000008
2398  00000000	 DB 00000000B,00000000B,00000000B,00000000B,00000000B,00000000B,00000000B,00000000B
      00000000
23A0          		END



Statistics:

     4	passes
     0	jr promotions
    60	symbols
   292	bytes

    60	macro calls
  3756	macro bytes
     0	invented symbols
