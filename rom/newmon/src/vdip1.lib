; util routines for accessing VDIP1
; caller must define 'vdbuf' as input buffer.
; 'cport' contains our port number base

vd$txe	equ	00000100b	; Tx ready
vd$rxr	equ	00001000b	; Rx data ready

vdprmp:	db	'D:\>',CR
rdf:	db	'rdf ',0,0,0,128,CR

; copy HL to DE, until NUL
strcpy:
	mov	a,m
	stax	d
	ora	a
	rz
	inx	h
	inx	d
	jr	strcpy

; compare DE to HL, until CR or NUL
strcmp:
	ldax	d
	cmp	m
	rnz
	ora	a
	rz
	cpi	CR
	rz
	inx	h
	inx	d
	jr	strcmp

; send command, wait for prompt or error
; HL=command string, CR term
vdcmd:	
	call	vdmsg
vdend:
	call	vdinp
	lxi	h,vdbuf
	lxi	d,vdprmp
	call	strcmp
	rz	; OK
	; error, always?
	stc
	ret

; read record (128 bytes) from file, into HL
; returns CY if error, else HL at "next" addr
vdrd:	push	h
	lxi	h,rdf
	call	vdmsg
	pop	h
	call	vdinb
	push	h
	call	vdinp
	lxi	h,vdbuf
	lxi	d,vdprmp
	call	strcmp
	pop	h	; "next" buffer addr
	rz
	stc
	ret

sync:	mvi	b,5
	mvi	a,'E'
	call	vdout
	mvi	a,CR
	call	vdout
	call	vdinp	; line to vdbuf
	rc
	lda	vdbuf
	cpi	'E'
	jrnz	sync0
	lda	vdbuf+1
	cpi	CR
	rz
sync0:	djnz	sync
	stc
	ret

; Observed timing:
; [0-562mS]
;	(cr)
;	Ver 03.68VDAPF On-Line:(cr)
; [250mS]
;	Device Detected P2(cr)
; [16-18mS]
;	No Upgrade(cr)
; [1-2mS]
;	D:\>(cr)
; Delays are measured between (cr)s, include all characters.
; The expectation is that the monitor is doing this before we
; even get here.

; get rid of any characters waiting... flush input
; Stop if we hit '>',CR
runout0:
	mov	e,a
runout:
	call	vdinz	; short timeout...
	rc		; done - nothing more to drain
	cpi	CR
	jrnz	runout0
	mov	a,e
	cpi	'>'
	jrnz	runout
	xra	a
	ret

;;;;;;;; everything else is private ;;;;;;;;;

; receive chars until CR, into vdbuf
; returns HL->[CR] (if NC)
vdinp:	lxi	h,vdbuf
vdi2:	call	vdinc
	rc
	mov	m,a
	cpi	CR
	rz
	inx	h
	jr	vdi2

; short-timeout input - for draining
vdinz:
	mvi	b,50		; 100mS timeout
	push	h
	push	d
	lxi	h,ticcnt	; use 2mS increments
	mov	e,m
	jr	vdix

; avoid hung situations
vdinc:
	mvi	b,6		; 2.5-3 second timeout
	push	h
	push	d
	lxi	h,ticcnt+1	; hi byte ticks at 512mS
	mov	e,m		; current tick...
vdix:	lda	cport
	adi	2	; vd$sts
	mov	c,a
vdi0:	inp	a	; vd$sts
	ani	vd$rxr
	jrnz	vdi1
	mov	a,m
	cmp	e
	jrz	vdi0
	mov	e,a
	djnz	vdi0
	pop	d
	pop	h
	stc
	ret
vdi1:	dcr	c	; vd$dat
	inp	a
	pop	d
	pop	h
	ret

; get read data.
; HL=buffer, length always 128
vdinb:	mvi	b,128
	lda	cport
	adi	2	; vd$sts
	mov	c,a
vdb0:	inp	a
	ani	vd$rxr
	jrz	vdb0
	dcr	c
	inp	a
	inr	c
	mov	m,a
	inx	h
	djnz	vdb0
	ret

; send char to VDIP1
; A=char
vdout:	push	b
	push	psw
	lda	cport
	adi	2	; vd$sts
	mov	c,a
vdo0:	inp	a
	ani	vd$txe
	jrz	vdo0
	pop	psw
	dcr	c
	outp	a
	pop	b
	ret

; HL=message, terminated by CR
vdmsg:
	lda	cport
	adi	2	; vd$sts
	mov	c,a
vdmsg0:	inp	a
	ani	vd$txe
	jrz	vdmsg0
	mov	a,m
	dcr	c
	outp	a
	inr	c
	cpi	CR	; CR
	rz
	inx	h
	jr	vdmsg0

; end of library
